// Depends on plugins:
//    id 'com.jfrog.bintray' version '1.4'
//    id 'org.ajoberstar.grgit' version '1.3.2'
//    id 'org.ajoberstar.github-pages' version '1.3.2'
// Depends on distribution.gradle

//  PUBLISH ARTIFACTS
// ==================
bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publish = true
    pkg {
        repo = 'xenon'
        name = 'xenon'
        desc = description
        userOrg = 'nlesc'
        licenses = ['Apache-2.0']
        websiteUrl = 'https://nlesc.github.io/Xenon'
        vcsUrl = 'https://github.com/NLeSC/Xenon.git'
        issueTrackerUrl = 'https://github.com/NLeSC/Xenon/issues'
    }
    publications = ['MyPublication']
}

tasks.publish.dependsOn 'bintrayUpload'

// PUBLISH DOCUMENTATION

task versionSite() {
    description 'Update Xenon version of site'
    group 'Publishing'
    def content = """current: ${version}
"""
    def siteVersionFn = "docs/_data/version.yml"
    doLast {
        new File(siteVersionFn).text = content
    }
    outputs.upToDateWhen {
        new File(siteVersionFn).text == content
    }
}

task copyJavadoc(type: Copy) {
    description 'Copy Xenon Java documentation to site'
    group 'Publishing'
    from "build/docs"
    into "docs/versions/${version}"
    dependsOn 'adaptorDocumentation', 'javadoc', 'javadocDevel'
}

task publishSite() {
    description 'Publishes artifacts to site (aka /docs/ directory)'
    group 'Publishing'
    dependsOn 'versionSite', 'copyJavadoc'
}

task publishToBintray() {
    dependsOn 'bintrayUpload',
        'publishDocGeneratorPublicationPublicationToDocGeneratorRepoRepository',
        'publishTestApiPublicationPublicationToTestApiRepoRepository'
}
